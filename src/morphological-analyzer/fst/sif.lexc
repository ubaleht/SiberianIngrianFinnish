Multichar_Symbols

+N +V

+Sg +Pl

+Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3

+Nom +Gen +Prt +Ill +Ine +Ela +All +Ade +Abl +Tra +Com

%{A%}

%{A1%}

LEXICON NomSgStem

+N+Nom+Sg:0 # ;

LEXICON OblSgStem

+N+Gen+Sg:n # ;
+N+Ine+Sg:s # ;
+N+Ela+Sg:st # ;
+N+All+Sg:l # ;
+N+Ade+Sg:l # ;
+N+Abl+Sg:lt # ;
+N+Tra+Sg:ks # ;
+N+Com+Sg:n'k%{A%} # ;
+N+Nom+Pl: t # ;

LEXICON PrtSgStem

+N+Prt+Sg:0 # ;

LEXICON IllSgStem

+N+Ill+Sg:0 # ;

LEXICON OblPlStem

+N+Gen+Pl:n # ;
+N+Prt+Pl:j # ;
+N+Ill+Pl:s # ;
+N+Ine+Pl:s # ;
+N+Ela+Pl:st # ;
+N+All+Pl:l # ;
+N+Ade+Pl:l # ;
+N+Abl+Pl:lt # ;
+N+Tra+Pl:ks # ;
+N+Com+Pl:n'k%{A%} # ;

LEXICON CS-2-All-Stems-Without-OblPlStem

:0 NomSgStem ;
:%{A%} OblSgStem ;
:%{A%} PrtSgStem ;
:%{A%} IllSgStem ;

LEXICON CS2-I ! Morphological type "CS2", alternation of the final vowel of the stem "a/ä" into "i" in OblPlStem. See Sidorkevich's PhD thesis, p. 172. 

CS-2-All-Stems-Without-OblPlStem ;
:i OblPlStem ;

LEXICON CS2-O ! Morphological type "CS2", alternation of the final vowel of the stem "a" into "o" and "ä" into "ö" in OblPlStem. See Sidorkevich's PhD thesis, p. 172. 

CS-2-All-Stems-Without-OblPlStem ;
:%{A1%} OblPlStem ;

LEXICON Root

Nouns ;
Verbs ;

LEXICON Nouns

NounStems ;

LEXICON NounStems

käsi:käsi NomSgStem ;
käsi:käe OblSgStem ;
käsi:kätt PrtSgStem ;
käsi:kätte IllSgStem ;
käsi:kässi OblPlStem ;
kukk:kukk NomSgStem ;
kukk:kuka OblSgStem ;
kukk:kukka PrtSgStem ;
kukk:kukka IllSgStem ;
kukk:kukki OblPlStem ;
razv:razv NomSgStem ;
razv:razva OblSgStem ;
razv:razva PrtSgStem ;
razv:razva IllSgStem ;
razv:razvo OblPlStem ;
uus:uus NomSgStem ;
uus:uvve OblSgStem ;
uus:uutt PrtSgStem ;
uus:uuvves IllSgStem ;
uus:uusi OblPlStem ;
jänis:jänis NomSgStem ;
jänis:jänikse OblSgStem ;
jänis:jänist PrtSgStem ;
jänis:jänikse IllSgStem ;
jänis:jäniksi OblPlStem ;
koir:koir CS2-I ;
vasar:vasar CS2-O ;


